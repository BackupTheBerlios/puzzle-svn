"Name"    = 'LALR(1) C# 2 nonunicode grammar'
"Version" = '1.0 alpha 110'
"Author"  = 'Roger Johansson'
"About"   = 'Based on Dmitry Gazko Java Grammar'

{String Char} = {Printable} - ["]
{Quote} = ['']
{IdLetter} = {Letter} + [_$]
{IdAlphaNumeric} = {Alphanumeric} + [_$]
{HexDigit} = {Digit} + [abcdefABCDEF]
{OctalCharSet} = [01234567]
{NoZeroDigit} = [123456789]
{LongTypeSuffix} =[lL]
{FloatTypeSuffix} =[dfDF]
{ExponentPartIndicator} = [eE]
{Sign} = [-+]
{CharSign} = [abtnfr"\] + {Quote}
{CharSign1} = {String Char} - [\]
{HexEscapeSign} =[uUxX]

Identifier     = {IdLetter}{IdAlphaNumeric}*
StringLiteral  = '"'{String Char}*'"'
FloatingPointLiteral   = {Digit}+'.'{Digit}+{FloatTypeSuffix}? | {Digit}+{FloatTypeSuffix} | '.'{Digit}+{FloatTypeSuffix}?
FloatingPointLiteralExponent = {Digit}+'.'{Digit}+{ExponentPartIndicator}{Sign}?{Digit}+{FloatTypeSuffix}? | {Digit}+{ExponentPartIndicator}{Sign}?{Digit}+{FloatTypeSuffix}? | '.'{Digit}+{ExponentPartIndicator}{Sign}?{Digit}+{FloatTypeSuffix}?
BooleanLiteral = 'true' | 'false'
IndirectCharLiteral = {Quote}{CharSign1}{Quote}
StandardEscapeCharLiteral = {Quote}'\'{CharSign}{Quote}
OctalEscapeCharLiteral ={Quote}'\'{OctalCharSet}+{Quote}
HexEscapeCharLiteral ={Quote}'\'{HexEscapeSign}{HexDigit}+{Quote}
NullLiteral = 'null'
StartWithNoZeroDecimalIntegerLiteral = {NoZeroDigit}{Digit}*{LongTypeSuffix}?
StartWithZeroDecimalIntegerLiteral = '0'{LongTypeSuffix}?
HexIntegerLiteral = '0'('x'|'X'){HexDigit}+{LongTypeSuffix}?
OctalIntegerLiteral = '0'{OctalCharSet}+{LongTypeSuffix}?


"Case Sensitive" = 'True'
"Start Symbol"   = <CompilationUnit>

Comment Start = '/*'
Comment End = '*/'
Comment Line = '//'
             | '#region'
             | '#endregion'

<CharacterLiteral> 
        ::= IndirectCharLiteral
         |  StandardEscapeCharLiteral
         |  OctalEscapeCharLiteral
         |  HexEscapeCharLiteral

<DecimalIntegerLiteral> 
        ::= StartWithZeroDecimalIntegerLiteral
         |  StartWithNoZeroDecimalIntegerLiteral


<FloatPointLiteral> 
        ::= FloatingPointLiteral
         |  FloatingPointLiteralExponent

<IntegerLiteral> 
        ::= <DecimalIntegerLiteral>
         |  HexIntegerLiteral  
         |  OctalIntegerLiteral


<Literal> 
        ::= <IntegerLiteral>
         |  <FloatPointLiteral>
         |  BooleanLiteral
         |  <CharacterLiteral>
         |  StringLiteral
         |  NullLiteral

        

<Type>  ::= <ArrayType>  
         |  <UserType>
         |  <PrimitiveType>           


<TypeList>
        ::= <Type>
         |  <TypeList> ',' <Type>


<PrimitiveType> ::= <NumericType>
                  | 'bool'

<NumericType> 
        ::= <IntegralType>
         |  <FloatingPointType>

<IntegralType>
        ::= 'byte'
         |  'short'
         |  'int'
         |  'long'
         |  'char'

<FloatingPointType> 
        ::= 'float'
         |  'double'

<UserType> ::= <Name>
             | <NullableType>
             | <GenericType>

<NullableType> ::= <PrimitiveType> '?'
                 | <Name> '?'


<ArrayType>
        ::= <PrimitiveType> '[' ']'
         |  <NullableType> '[' ']'
         |  <Name> '[' ']'
         |  <ArrayType> '[' ']'
         |  <GenericType> '[' ']'

<GenericType> 
        ::= <Name> '<'<TypeList>'>'

<Name>
        ::= <SimpleName>
         |  <QualifiedName>

<SimpleName> ::= Identifier

<QualifiedName> ::= Identifier '.' <Name>

<CompilationUnit> 
        ::=  <Codes>

<Codes> ::=  <Code> <Codes>
	  |  <Code>

<Code>	::= <TypeDeclaration>
	  | <NamespaceDeclaration>
	  | <UsingDeclaration>
	  | <AliasDeclaration>

<NamespaceDeclaration>		::= <NamespaceDeclarationHeader> <NamespaceDeclarationBody>

<NamespaceDeclarationHeader> 	::= 'namespace' <Name> 
<NamespaceDeclarationBody>		::= '{' '}'
					| '{' <Codes> '}'

<UsingDeclaration>
        ::= 'using' <Name> ';'


<AliasDeclaration>
	 ::= 'using' <SimpleName> '=' <Name> ';'


<TypeDeclaration> 
        ::= <ClassDeclaration>
         |  <InterfaceDeclaration>
         |  <StructDeclaration>
         |  <EnumDeclaration>
         |  <DelegateDeclaration>
         |  ';'

<Modifiers> 
        ::= <Modifier>
         |  <Modifiers> <Modifier>

<Modifier> 
        ::= 'public'
         |  'protected'
         |  'private'
         |  'static'
         |  'abstract'
         |  'sealed'
         |  'unsafe'
         |  'internal'
         |  'volatile'

<ClassDeclaration> 
        ::= <AttributeGroups> <Modifiers> 'class' <GenericIdentifier> <Inherits>  <ClassBody>
          | <AttributeGroups> <Modifiers> 'class' <GenericIdentifier> <ClassBody>
          | <AttributeGroups> 'class' <GenericIdentifier> <Inherits> <ClassBody>
          | <AttributeGroups> 'class' <GenericIdentifier> <ClassBody>
          | <Modifiers> 'class' <GenericIdentifier> <Inherits>  <ClassBody>
          | <Modifiers> 'class' <GenericIdentifier> <ClassBody>
          | 'class' <GenericIdentifier> <Inherits> <ClassBody>
          | 'class' <GenericIdentifier> <ClassBody>


<Inherits> ::= ':' <ClassTypeList>



<ClassBody> 
        ::= '{' '}'
         |  '{' <ClassBodyDeclarations> '}'
               

<ClassBodyDeclarations> 
        ::= <ClassBodyDeclaration>
	 |  <ClassBodyDeclarations> <ClassBodyDeclaration>
           

<ClassBodyDeclaration> 
        ::= <ClassMemberDeclaration>
         |  <ConstructorDeclaration>

<ClassMemberDeclaration>
        ::= <ClassFieldDeclaration>
         |  <PropertyDeclaration>
         |  <IndexerDeclaration>
         |  <MethodDeclaration>

<StructMemberDeclaration>
        ::= <StructFieldDeclaration>
         |  <PropertyDeclaration>
         |  <IndexerDeclaration>
         |  <MethodDeclaration>

<ClassFieldDeclaration> 
        ::= <Modifiers> <Type> <ClassVariableDeclarators> ';'
         |  <Type> <ClassVariableDeclarators> ';'

<StructFieldDeclaration> 
        ::= <Modifiers> <Type> <StructVariableDeclarators> ';'
         |  <Type> <StructVariableDeclarators> ';'

<ClassVariableDeclarators>
        ::= <ClassVariableDeclarator>
         |  <ClassVariableDeclarators> ',' <ClassVariableDeclarator>

<StructVariableDeclarators>
        ::= <StructVariableDeclarator>
         |  <StructVariableDeclarators> ',' <StructVariableDeclarator>

<ClassVariableDeclarator> 
        ::= <VariableDeclaratorId>
         |  <VariableDeclaratorId> '=' <VariableInitializer>

<StructVariableDeclarator> 
        ::= <VariableDeclaratorId>

<VariableDeclaratorId> 
        ::= Identifier

<VariableInitializer> 
        ::= <Expression>
         |  <ArrayInitializer>

<MethodDeclaration> ::= <AttributeGroups> <MethodHeader> <MethodBody>
                      | <MethodHeader> <MethodBody>

<MethodHeader>
        ::= <Modifiers> <Type> <MethodDeclarator>
         |  <Type> <MethodDeclarator>

<MethodDeclarator>
        ::= <Name> '(' <FormalParameterList> ')'
         |  <Name> '(' ')'
         |  <Name> '<' <TypeList> '>' '(' <FormalParameterList> ')'
         |  <Name> '<' <TypeList> '>' '(' ')'


<FormalParameterList> 
        ::= <FormalParameter>
         |  <FormalParameterList> ',' <FormalParameter>

<FormalParameter> 
        ::= <AttributeGroups> <StandardParameter>
	   | <AttributeGroups> <SpecialFormalParameter> 
          | <StandardParameter>
	   | <SpecialFormalParameter> 



<StandardParameter>
	  ::= <Type> <VariableDeclaratorId>

<SpecialFormalParameter> ::= <ParamsFormalParameter>
		 | <RefFormalParameter>
		 | <OutFormalParameter>

<ParamsFormalParameter> ::= 'params' <ArrayType> <VariableDeclaratorId>
<RefFormalParameter> ::= 'ref' <Type> <VariableDeclaratorId>
<OutFormalParameter> ::= 'out' <Type> <VariableDeclaratorId>


<PropertyDeclaration> ::= <AttributeGroups> <PropertyHeader> <PropertyBody>
                        | <PropertyHeader> <PropertyBody>

<PropertyHeader>
        ::= <Modifiers> <Type> <PropertyDeclarator>
         |  <Type> <PropertyDeclarator>

<PropertyDeclarator>
        ::= Identifier 

<PropertyBody>
	 ::= '{' <PropertyGetPart> <PropertySetPart> '}'
	 |   '{' <PropertyGetPart> '}'
	 |   '{' <PropertySetPart> '}'

<PropertyGetPart>
	 ::= <AttributeGroups> 'get' <Block>
          | <AttributeGroups> 'get' ';'
          | 'get' <Block>
          | 'get' ';'

<PropertySetPart>
	 ::= <AttributeGroups> 'set' <Block>
          | <AttributeGroups> 'set' ';'
          | 'set' <Block>
          | 'set' ';'

<IndexerDeclaration> ::= <AttributeGroups> <IndexerHeader> <IndexerBody>
                       | <IndexerHeader> <IndexerBody>

<IndexerHeader>
        ::= <Modifiers> <Type> <IndexerDeclarator>
         |  <Type> <IndexerDeclarator>

<IndexerDeclarator>
        ::= this '[' <FormalParameterList> ']'


<IndexerBody>
	 ::= '{' <IndexerGetPart> <IndexerSetPart> '}'
	 |   '{' <IndexerGetPart> '}'
	 |   '{' <IndexerSetPart> '}'

<IndexerGetPart>
	 ::= <AttributeGroups> 'get' <Block>
          | <AttributeGroups> 'get' ';'
          | 'get' <Block>
          | 'get' ';'

<IndexerSetPart>
	 ::= <AttributeGroups> 'set' <Block>
          | <AttributeGroups> 'set' ';'
          | 'set' <Block>
          | 'set' ';'


<ClassTypeList>
        ::= <UserType>
         |  <ClassTypeList> ',' <UserType>


<MethodBody> 
        ::= <Block>
         |  ';'

<ConstructorDeclaration>
        ::= <AttributeGroups> <Modifiers> <ConstructorDeclarator> <ConstructorBody>
          | <AttributeGroups> <ConstructorDeclarator> <ConstructorBody>
          | <Modifiers> <ConstructorDeclarator> <ConstructorBody>
          | <ConstructorDeclarator> <ConstructorBody>


<ConstructorDeclarator>
        ::= <SimpleName> '(' <FormalParameterList> ')'
         |  <SimpleName> '(' ')'
         |  <SimpleName> '(' <FormalParameterList> ')' ':' <ExplicitConstructorInvocation>
         |  <SimpleName> '(' ')' ':' <ExplicitConstructorInvocation>


<StructConstructorDeclaration>
        ::= <AttributeGroups> <Modifiers> <StructConstructorDeclarator> <ConstructorBody>
          | <AttributeGroups> <StructConstructorDeclarator> <ConstructorBody>
          | <Modifiers> <StructConstructorDeclarator> <ConstructorBody>
          | <StructConstructorDeclarator> <ConstructorBody>


<StructConstructorDeclarator>
        ::= <SimpleName> '(' <FormalParameterList> ')'
         | <SimpleName> '(' <FormalParameterList> ')' ':' <ExplicitConstructorInvocation>

<ConstructorBody> 
        ::= '{' <BlockStatements> '}'
         |  '{' '}'

<ExplicitConstructorInvocation>
        ::= 'this' '(' <ArgumentList> ')' 
         |  'this' '(' ')' 
         |  'base' '(' <ArgumentList> ')'
         |  'base' '(' ')' 
                                   
<InterfaceDeclaration> 
        ::= <AttributeGroups> <Modifiers> 'interface' <GenericIdentifier> <Inherits> <InterfaceBody>
         | <AttributeGroups> <Modifiers> 'interface' <GenericIdentifier> <InterfaceBody>
         | <AttributeGroups> 'interface' <GenericIdentifier> <Inherits> <InterfaceBody>
         | <AttributeGroups> 'interface' <GenericIdentifier> <InterfaceBody>
         | <Modifiers> 'interface' <GenericIdentifier> <Inherits> <InterfaceBody>
         | <Modifiers> 'interface' <GenericIdentifier> <InterfaceBody>
         | 'interface' <GenericIdentifier> <Inherits> <InterfaceBody>
         | 'interface' <GenericIdentifier> <InterfaceBody>


<StructDeclaration> 
        ::= <AttributeGroups> <Modifiers> 'struct' <GenericIdentifier> <Inherits> <StructBody>
         | <AttributeGroups> <Modifiers> 'struct' <GenericIdentifier> <StructBody>
         | <AttributeGroups> 'struct' <GenericIdentifier> <Inherits> <StructBody>
         | <AttributeGroups> 'struct' <GenericIdentifier> <StructBody>
         | <Modifiers> 'struct' <GenericIdentifier> <Inherits> <StructBody>
         | <Modifiers> 'struct' <GenericIdentifier> <StructBody>
         | 'struct' <GenericIdentifier> <Inherits> <StructBody>
         | 'struct' <GenericIdentifier> <StructBody>


<StructBody> 
        ::= '{' '}'
         |  '{' <StructBodyDeclarations> '}'

<StructBodyDeclarations> 
        ::= <StructBodyDeclaration>
	 |  <StructBodyDeclarations> <StructBodyDeclaration>

<StructBodyDeclaration> 
        ::= <StructMemberDeclaration>
         |  <StructConstructorDeclaration>

<InterfaceBody> 
        ::= '{' <InterfaceMemberDeclarations> '}'
         |  '{' '}'

<InterfaceMemberDeclarations> 
        ::= <InterfaceMemberDeclaration>
         |  <InterfaceMemberDeclarations> <InterfaceMemberDeclaration>

<InterfaceMemberDeclaration> 
        ::= <ConstantDeclaration>
         |  <AbstractMethodDeclaration>

<ConstantDeclaration> ::= <ClassFieldDeclaration>

<AbstractMethodDeclaration> ::= <MethodHeader> ';'

<ArrayInitializer>
        ::= '{' <VariableInitializers> ',' '}'
         |  '{' <VariableInitializers> '}'
         |  '{' ',' '}'
         |  '{' '}'

<VariableInitializers> ::= <VariableInitializer>
                        | <VariableInitializers> ',' <VariableInitializer>

<Block> ::= '{' <BlockStatements> '}'
         |  '{' '}'

<BlockStatements> 
        ::= <BlockStatement>
         |  <BlockStatements> <BlockStatement>

<BlockStatement>
        ::= <LocalVariableDeclarationStatement>
         |  <Statement>

<LocalVariableDeclarationStatement> 
        ::= <LocalVariableDeclaration> ';'

<LocalVariableDeclaration> 
        ::= <Type> <ClassVariableDeclarators>

<Statement>
        ::= <StatementWithoutTrailingSubstatement>
         |  <LabeledStatement>
         |  <IfThenStatement>
         |  <IfThenElseStatement>
         |  <WhileStatement>
         |  <ForStatement>
         |  <ForEachStatement>
	  |  <UsingStatement>

<UsingStatement>
	 ::= 'using' '(' <UserType> <VariableDeclaratorId> '=' <Expression> ')' <Statement>

<StatementNoShortIf> 
        ::= <StatementWithoutTrailingSubstatement>
         |  <LabeledStatementNoShortIf>
         |  <IfThenElseStatementNoShortIf>
         |  <WhileStatementNoShortIf>
         |  <ForStatementNoShortIf>
         |  <ForEachStatementNoShortIf> 

<StatementWithoutTrailingSubstatement> 
        ::= <Block>
         |  <EmptyStatement>
         |  <ExpressionStatement>
         |  <SwitchStatement>
         |  <DoStatement>
         |  <BreakStatement>
         |  <ContinueStatement>
         |  <ReturnStatement>
         |  <YieldReturnStatement>
         |  <SynchronizedStatement>
         |  <ThrowStatement>
         |  <TryStatement>

<EmptyStatement> 
        ::= ';'

<LabeledStatement> 
        ::= Identifier ':' <Statement>

<LabeledStatementNoShortIf> 
        ::= Identifier ':' <StatementNoShortIf>

<ExpressionStatement> 
        ::= <StatementExpression> ';'

<StatementExpression>
        ::= <Assignment>
         |  <PreIncrementExpression>
         |  <PreDecrementExpression>
         |  <PostIncrementExpression>
         |  <PostDecrementExpression>
         |  <MethodInvocation>
         |  <ClassInstanceCreationExpression>

<IfThenStatement> 
        ::= 'if' '(' <Expression> ')' <Statement>

<IfThenElseStatement> 
        ::= 'if' '(' <Expression> ')' <StatementNoShortIf> 'else' <Statement>

<IfThenElseStatementNoShortIf> 
        ::= 'if' '(' <Expression> ')' <StatementNoShortIf> 'else' <StatementNoShortIf>

<SwitchStatement> 
        ::= 'switch' '(' <Expression> ')' <SwitchBlock>

<SwitchBlock> 
        ::= '{' <SwitchBlockStatementGroups> <SwitchLabels> '}'
         |  '{' <SwitchBlockStatementGroups> '}'
         |  '{' <SwitchLabels> '}'
         |  '{' '}'

<SwitchBlockStatementGroups>
        ::= <SwitchBlockStatementGroup>
         |  <SwitchBlockStatementGroups> <SwitchBlockStatementGroup>

<SwitchBlockStatementGroup> 
        ::= <SwitchLabels> <BlockStatements>

<SwitchLabels>
        ::= <SwitchLabel>
         |  <SwitchLabels> <SwitchLabel>

<SwitchLabel> 
        ::= 'case' <ConstantExpression> ':'
         |  'default' ':'

<WhileStatement> 
        ::= 'while' '(' <Expression> ')' <Statement>

<WhileStatementNoShortIf>
        ::= 'while' '(' <Expression> ')' <StatementNoShortIf>

<DoStatement> 
        ::= 'do' <Statement> 'while' '(' <Expression> ')' ';'

<ForStatement> 
        ::= 'for' '(' <ForInit> ';' <Expression> ';' <ForUpdate> ')' <Statement>
         |  'for' '(' <ForInit> ';' <Expression> ';' ')' <Statement>
         |  'for' '(' <ForInit> ';' ';' <ForUpdate> ')' <Statement>
         |  'for' '(' <ForInit> ';' ';' ')' <Statement>
         |  'for' '(' ';' <Expression> ';' <ForUpdate> ')' <Statement>
         |  'for' '(' ';' <Expression> ';' ')' <Statement>
         |  'for' '(' ';' ';' <ForUpdate> ')' <Statement>
         |  'for' '(' ';' ';'  ')' <Statement>

<ForEachStatement> 
	 ::= 'foreach' '(' <Type> <SimpleName> 'in' <Expression> ')' <Statement>

<ForEachStatementNoShortIf> 
	 ::= 'foreach' '(' <Type> <SimpleName> 'in' <Expression> ')' <StatementNoShortIf>

<ForStatementNoShortIf> 
        ::= 'for' '(' <ForInit> ';' <Expression> ';' <ForUpdate> ')' <StatementNoShortIf>
         |  'for' '(' <ForInit> ';' <Expression> ';' ')' <StatementNoShortIf>
         |  'for' '(' <ForInit> ';' ';' <ForUpdate> ')' <StatementNoShortIf>
         |  'for' '(' <ForInit> ';' ';' ')' <StatementNoShortIf>
         |  'for' '(' ';' <Expression> ';' <ForUpdate> ')' <StatementNoShortIf>
         |  'for' '(' ';' <Expression> ';' ')' <StatementNoShortIf>
         |  'for' '(' ';' ';' <ForUpdate> ')' <StatementNoShortIf>
         |  'for' '(' ';' ';' ')' <StatementNoShortIf>

<ForInit> ::= <StatementExpressionList>
            | <LocalVariableDeclaration>

<ForUpdate> ::= <StatementExpressionList>

<StatementExpressionList> 
        ::= <StatementExpression>
         |  <StatementExpressionList> ',' <StatementExpression>

<BreakStatement> 
        ::= 'break' Identifier ';'
         |  'break' ';'

<ContinueStatement>
        ::= 'continue' Identifier ';'
         |  'continue' ';'

<ReturnStatement> 
        ::= 'return' <Expression> ';'
         |  'return' ';'

<YieldReturnStatement> 
        ::= 'yield' 'return' <Expression> ';'

<ThrowStatement> 
        ::= 'throw' <Expression> ';'
          | 'throw' ';'

<SynchronizedStatement> 
        ::= 'synchronized' '(' <Expression> ')' <Block>

<TryStatement>
        ::= 'try' <Block> <Catches>
	  |  'try' <Block> <CatchDefault>
	  |  'try' <Block> <Catches> <CatchDefault>
         |  'try' <Block> <Catches> <Finally>
         |  'try' <Block> <Catches> <CatchDefault> <Finally>
         |  'try' <Block> <CatchDefault> <Finally>
         |  'try' <Block> <Finally>

<Catches>
        ::= <CatchClause>
         |  <Catches> <CatchClause>

<CatchClause>
        ::= 'catch' '(' <FormalParameter> ')' <Block>

<CatchDefault>
        ::= 'catch' <Block>

<Finally> 
        ::= 'finally' <Block>

!Note UserType is not included here since there is no way to distinguish between such type and a property path at parselevel
<PrimaryOrType> ::= <PrimitiveType>
                  | <GenericType>
                  | <Primary>

<Primary> 
        ::= <PrimaryNoNewArray>
         |  <ArrayCreationExpression>

<PrimaryNoNewArray> 
        ::= <Literal>
         |  'this'
         |  'base'
         |  '(' <Expression> ')'
         |  <ClassInstanceCreationExpression>
         |  <FieldAccess>
         |  <TypeOf>
         |  <MethodInvocation>
         |  <ArrayAccess>




<TypeOf>
	::= 'typeof' '(' <Type> ')'


<ClassInstanceCreationExpression> 
        ::= 'new' <UserType> '(' <ArgumentList> ')'
         |  'new' <UserType> '(' ')'

<ArgumentList> 
        ::= <SpecialArg>
         |  <ArgumentList> ',' <SpecialArg>
	  |  <Expression>
         |  <ArgumentList> ',' <Expression>

<SpecialArg> ::= <ParamsArg>
		 | <RefArg>
		 | <OutArg>

<ParamsArg> ::= 'params' Identifier
<RefArg> ::= 'ref' Identifier
<OutArg> ::= 'out' Identifier

 

<ArrayCreationExpression> 
        ::= 'new' <PrimitiveType> <DimExprs> <Dims>
         |  'new' <PrimitiveType> <DimExprs>
         |  'new' <UserType> <DimExprs> <Dims>
         |  'new' <UserType> <DimExprs>

<DimExprs> 
        ::= <DimExpr>
         |  <DimExprs> <DimExpr>

<DimExpr> ::= '[' <Expression> ']'

<Dims> ::= '[' ']'
         | <Dims> '[' ']'

<FieldAccess> 
        ::= <PrimaryOrType> '.' Identifier

<MethodInvocation>
        ::= <Name> '(' <ArgumentList> ')'
         |  <Name> '(' ')'
         |  <PrimaryOrType> '.' Identifier '(' <ArgumentList> ')'
         |  <PrimaryOrType> '.' Identifier '(' ')'



<ArrayAccess> 
        ::= <Name> '[' <Expression> ']'
         |  <PrimaryNoNewArray> '[' <Expression> ']'

<PostfixExpression>
        ::= <Primary>
         |  <Name>
         |  <PostIncrementExpression>
         |  <PostDecrementExpression>



<PostIncrementExpression>
        ::= <PostfixExpression> '++'

<PostDecrementExpression>
        ::= <PostfixExpression> '--'

<UnaryExpression>
        ::= <PreIncrementExpression>
         |  <PreDecrementExpression>
         |  '+' <UnaryExpression>
         |  '-' <UnaryExpression>
         |  <UnaryExpressionNotPlusMinus>
         |  <AsExpression>

<PreIncrementExpression> 
        ::= '++' <UnaryExpression>

<PreDecrementExpression>
        ::= '--' <UnaryExpression>

<UnaryExpressionNotPlusMinus> 
        ::= <PostfixExpression>
         |  '~' <UnaryExpression>
         |  '!' <UnaryExpression>
         |  <CastExpression>

<CastExpression>
        ::= '(' <PrimitiveType> <Dims> ')' <UnaryExpression>
         |  '(' <PrimitiveType> ')' <UnaryExpression>
         |  '(' <Expression> ')' <UnaryExpressionNotPlusMinus>
         |  '(' <Name> <Dims> ')' <UnaryExpressionNotPlusMinus>

<AsExpression>
	 ::= <UnaryExpressionNotPlusMinus> 'as' <GenericType>
          | <UnaryExpressionNotPlusMinus> 'as' <ArrayType>
          | <UnaryExpressionNotPlusMinus> 'as' <Name>


<MultiplicativeExpression>
        ::= <UnaryExpression>
         |  <MultiplicativeExpression> '*' <UnaryExpression>
         |  <MultiplicativeExpression> '/' <UnaryExpression>
         |  <MultiplicativeExpression> '%' <UnaryExpression>

<AdditiveExpression>
        ::= <MultiplicativeExpression>
         |  <AdditiveExpression> '+' <MultiplicativeExpression>
         |  <AdditiveExpression> '-' <MultiplicativeExpression>

<ShiftExpression>
        ::= <AdditiveExpression>
         |  <ShiftExpression> '<<' <AdditiveExpression>
         |  <ShiftExpression> '>>' <AdditiveExpression>

<RelationalExpression>
        ::= <ShiftExpression>
         |  <RelationalExpression> '<' <ShiftExpression>
         |  <RelationalExpression> '>' <ShiftExpression>
         |  <RelationalExpression> '<=' <ShiftExpression>
         |  <RelationalExpression> '>=' <ShiftExpression>
         |  <RelationalExpression> 'is' <Type>

<EqualityExpression>
        ::= <RelationalExpression>
         |  <EqualityExpression> '==' <RelationalExpression>
         |  <EqualityExpression> '!=' <RelationalExpression>

<AndExpression>
        ::= <EqualityExpression>
          |  <AndExpression> '&' <EqualityExpression>

<ExclusiveOrExpression>
        ::= <AndExpression>
         |  <ExclusiveOrExpression> '^' <AndExpression>

<InclusiveOrExpression> 
        ::= <ExclusiveOrExpression>
         |  <InclusiveOrExpression> '|' <ExclusiveOrExpression>

<ConditionalAndExpression>
        ::= <InclusiveOrExpression>
         |  <ConditionalAndExpression> '&&' <InclusiveOrExpression>

<ConditionalOrExpression>
        ::= <ConditionalAndExpression>
         |  <ConditionalOrExpression> '||' <ConditionalAndExpression>

<ConditionalExpression> 
        ::= <ConditionalOrExpression>
         |  <ConditionalOrExpression> '?' <Expression> ':' <ConditionalExpression>

<AssignmentExpression> 
        ::= <ConditionalExpression>
         |  <Assignment>

<Assignment>
        ::= <LeftHandSide> <AssignmentOperator> <AssignmentExpression>

<LeftHandSide> 
        ::= <Name>
         |  <FieldAccess>
         |  <ArrayAccess>

<AssignmentOperator> 
        ::= '='
         |  '*='
         |  '/='
         |  '%='
         |  '+='
         |  '-='
         |  '<<='
         |  '>>='
         |  '&='
         |  '^='
         |  '|='

<Expression> ::= <AssignmentExpression>

<ConstantExpression> ::= <Expression>

<EnumDeclaration> ::= <AttributeGroups> <Modifiers> 'enum' Identifier <EnumInherits> <EnumBody>
                    | <AttributeGroups> <Modifiers> 'enum' Identifier <EnumBody>
                    | <AttributeGroups> 'enum' Identifier <EnumInherits> <EnumBody>
                    | <AttributeGroups> 'enum' Identifier <EnumBody>
		      | <Modifiers> 'enum' Identifier <EnumInherits> <EnumBody>
                    | <Modifiers> 'enum' Identifier <EnumBody>
                    | 'enum' Identifier <EnumInherits> <EnumBody>
                    | 'enum' Identifier <EnumBody>



<EnumInherits> ::= ':' <IntegralType>

<EnumBody> 
        ::= '{' '}'
         |  '{' <EnumBodyDeclarations> '}'
         |  '{' <EnumBodyDeclarations> ',' '}'



<EnumBodyDeclarations> ::= <EnumBodyDeclaration>
                         | <EnumBodyDeclarations> ',' <EnumBodyDeclaration>

<EnumBodyDeclaration> ::= <AttributeGroups> Identifier
                        | <AttributeGroups> Identifier '=' <IntegerLiteral>
                        | Identifier
                        | Identifier '=' <IntegerLiteral>




<DelegateDeclaration> ::= <AttributeGroups> <Modifiers> delegate <Type> <GenericIdentifier> '(' <FormalParameterList> ')' ';'
                        | <AttributeGroups> <Modifiers> delegate <Type> <GenericIdentifier> '(' ')' ';'
                        | <AttributeGroups> delegate <Type> <GenericIdentifier> '(' <FormalParameterList> ')' ';'
                        | <AttributeGroups> delegate <Type> <GenericIdentifier> '(' ')' ';'
                        | <Modifiers> delegate <Type> <GenericIdentifier> '(' <FormalParameterList> ')' ';'
                        | <Modifiers> delegate <Type> <GenericIdentifier> '(' ')' ';'
                        | delegate <Type> <GenericIdentifier> '(' <FormalParameterList> ')' ';'
                        | delegate <Type> <GenericIdentifier> '(' ')' ';'



! represents a type name generic or not generic (eg class MyClass<T>)
<GenericIdentifier> ::= Identifier
                   | Identifier '<' <TypeList> '>'

<AttributeGroups>
		::= <AttributeGroup>
		  | <AttributeGroups> <AttributeGroup>
<AttributeGroup>
		::= '[' <Attributes> ']'

<Attributes>
		::= <Attribute>
                | <Attributes> ',' <Attribute>

!TODO parameterized attributes (with property set support)
<Attribute>
		::= <Name>
                | <Name> '(' ')'



                